#version 460

#include "c_shared/mesh_shading_defines.h"
#include "global_descriptors.glsl"
#include "lib/culling.glsl"
#include "lib/intrinsics.glsl"

DEFINE_STANDARD_BUFFER_REFERENCE( 4, MeshCullDataBuffer, MeshCullData );
DEFINE_STANDARD_BUFFER_REFERENCE( 4, MeshletDrawCommandBuffer, MeshletDrawCommand );

layout( scalar, push_constant ) uniform PushConstants
{
    MeshCullDataBuffer meshCullDataBuffer;
    UintBuffer outputCountBuffer;
    MeshletDrawCommandBuffer indirectCmdOutputBuffer;
    uint numMeshes;
    uint _pad;
} pConstants;

layout (local_size_x = 64, local_size_y = 1) in;
void main() 
{
    uint meshIdx = gl_GlobalInvocationID.x;
    if ( meshIdx >= pConstants.numMeshes )
        return;
    
    MeshCullData cullData = pConstants.meshCullDataBuffer.data[meshIdx];
    AABB localAABB = cullData.aabb;
    AABB worldAABB = TransformAABB( GetModelMatrix( cullData.modelIndex ), localAABB );
    bool visible = IsInFrustum( worldAABB );
    uint numVisibleInWave = WaveActiveCountBits( visible );
    uint localIndexInWave = WavePrefixCountBits( visible );
    
    uint globalBaseIndex;
    if ( WaveIsFirstLane() )
    {
        globalBaseIndex = atomicAdd( pConstants.outputCountBuffer.data[0], numVisibleInWave );
    }
    globalBaseIndex = WaveReadLaneFirst( globalBaseIndex );
    
    if ( visible )
    {
        MeshletDrawCommand cmd;
#if TASK_SHADER_MAIN_DRAW
        cmd.groupCountX = ( cullData.numMeshlets + TASK_SHADER_WORKGROUP_SIZE - 1 ) / TASK_SHADER_WORKGROUP_SIZE;
#else // #if TASK_SHADER_MAIN_DRAW
        cmd.groupCountX = cullData.numMeshlets;
#endif // #else // #if TASK_SHADER_MAIN_DRAW
        cmd.groupCountY = 1;
        cmd.groupCountZ = 1;
        cmd.modelIndex  = cullData.modelIndex;
        cmd.meshIndex   = meshIdx;
        cmd.numMeshlets = cullData.numMeshlets;

        uint globalIndex = globalBaseIndex + localIndexInWave;
        pConstants.indirectCmdOutputBuffer.data[globalIndex] = cmd;        
    }
}