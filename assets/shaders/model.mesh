#version 450

#extension GL_EXT_mesh_shader : require

#include "global_descriptors.glsl"
#include "c_shared/model.h"
#include "c_shared/cull_data.h"

DEFINE_STANDARD_BUFFER_REFERENCE( 32, MeshletBuffer, Meshlet );
DEFINE_STANDARD_BUFFER_REFERENCE( 4, MeshDataBuffer, PerObjectData );
DEFINE_STANDARD_BUFFER_REFERENCE( 4, MeshletDrawCommandBuffer, MeshletDrawCommand );

#if INDIRECT

#extension GL_ARB_shader_draw_parameters : enable
layout(scalar, push_constant) uniform Registers
{
    MeshDataBuffer meshDataBuffer;
    MeshletDrawCommandBuffer indirectMeshletDrawBuffer;
};

#else // INDIRECT

layout(scalar, push_constant) uniform Registers
{
    PerObjectData objectData;
};

#endif // #else // INDIRECT

Meshlet GetMeshlet( uint meshletID, uint baseBuffer )
{
    uint64_t ptr = bindlessBuffers[baseBuffer + MESH_BUFFER_MESHLETS];
    MeshletBuffer mBuffer = MeshletBuffer( ptr );
    return mBuffer.data[meshletID];
}

uvec3 GetTri( const Meshlet meshlet, uint baseBuffer, uint index )
{
    uint64_t ptr = bindlessBuffers[baseBuffer + MESH_BUFFER_TRIS];
    UintBuffer tBuffer = UintBuffer( ptr );
    uint packedTri = tBuffer.data[meshlet.triOffset + index];
    uvec3 tri;
    tri.x = (packedTri >> 0)  & 0xFF;
    tri.y = (packedTri >> 8)  & 0xFF;
    tri.z = (packedTri >> 16) & 0xFF;
    return tri;
}

vec3 GetPos( const Meshlet meshlet, uint baseBuffer, uint index )
{
    uint64_t ptr = bindlessBuffers[baseBuffer + MESH_BUFFER_POSITIONS];
    Vec3Buffer buff = Vec3Buffer( ptr );
    return buff.data[meshlet.vertexOffset + index];
}

vec4 GetNormal( const Meshlet meshlet, uint baseBuffer, uint index )
{
    uint64_t ptr = bindlessBuffers[baseBuffer + MESH_BUFFER_NORMALS];
    Vec3Buffer buff = Vec3Buffer( ptr );
    return vec4( buff.data[meshlet.vertexOffset + index], 0 );
}

vec2 GetUV( const Meshlet meshlet, uint baseBuffer, uint index )
{
    uint64_t ptr = bindlessBuffers[baseBuffer + MESH_BUFFER_UVS];
    if ( ptr == 0 )
        return vec2( 0 );
    
    Vec2Buffer buff = Vec2Buffer( ptr );
    return buff.data[meshlet.vertexOffset + index];
}

vec4 GetTangentAndSign( const Meshlet meshlet, uint baseBuffer, uint index )
{
    uint64_t ptr = bindlessBuffers[baseBuffer + MESH_BUFFER_TANGENTS];
    if ( ptr == 0 )
        return vec4( 0 );
    
    Vec4Buffer buff = Vec4Buffer( ptr );
    return buff.data[meshlet.vertexOffset + index];
}

#if USE_TASK_SHADER

struct Task
{
    uint meshIndex;   
    uint baseID;
    uint8_t deltaIDs[TASK_SHADER_WORKGROUP_SIZE];
};

taskPayloadSharedEXT Task IN;

#endif // #if USE_TASK_SHADER

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 124) out;

layout (location = 0) out PerVertexData
{
    vec3 worldPos;
    vec3 normal;
    vec4 tangentAndSign;
    vec2 uv;
    flat uint materialIdx;
#if IS_DEBUG_SHADER
    flat uint meshletIdx;
#endif // #if IS_DEBUG_SHADER
} v_out[];

void main()
{
#if USE_TASK_SHADER
    const uint meshletID = IN.baseID + IN.deltaIDs[gl_WorkGroupID.x];
    #if INDIRECT
        const uint meshIndex = IN.meshIndex;
    #endif // #if INDIRECT
#else // #if USE_TASK_SHADER
    const uint meshletID = gl_WorkGroupID.x;
    #if INDIRECT
        const uint meshIndex = indirectMeshletDrawBuffer.data[gl_DrawIDARB].meshIndex;
    #endif // #if INDIRECT
#endif // #else // #if USE_TASK_SHADER
    
    const uint threadID = gl_LocalInvocationIndex;
    
    #if INDIRECT
        PerObjectData objectData = meshDataBuffer.data[meshIndex];
    #endif // #if INDIRECT
    
    const uint baseBuffer = objectData.bindlessRangeStart;

    Meshlet meshlet = GetMeshlet( meshletID, baseBuffer );
    if ( threadID == 0 )
    {
        SetMeshOutputsEXT( meshlet.vertexCount, meshlet.triangleCount );
    }

    if ( threadID < meshlet.vertexCount )
    {
        mat4 modelMatrix = GetModelMatrix( objectData.modelIdx );
        
        vec4 localPos = vec4( GetPos( meshlet, baseBuffer, threadID ), 1 );
        vec4 worldPos = modelMatrix * localPos;
        v_out[threadID].worldPos = worldPos.xyz;
        gl_MeshVerticesEXT[threadID].gl_Position = globals.VP * worldPos;
        
        vec4 localNormal = GetNormal( meshlet, baseBuffer, threadID );
        v_out[threadID].normal = (GetNormalMatrix( objectData.modelIdx ) * localNormal).xyz;
        
        v_out[threadID].uv = GetUV( meshlet, baseBuffer, threadID );
        
        vec4 tangentAndSign = GetTangentAndSign( meshlet, baseBuffer, threadID );
        vec3 worldTangent = (modelMatrix * vec4( tangentAndSign.xyz, 0 )).xyz;
        v_out[threadID].tangentAndSign = vec4( worldTangent, tangentAndSign.w );
        
        v_out[threadID].materialIdx = objectData.materialIdx;
    #if IS_DEBUG_SHADER
        v_out[threadID].meshletIdx = meshletID;
    #endif // #if IS_DEBUG_SHADER
    }
    
    uint tri1_ID = 2 * threadID + 0;
    if ( tri1_ID < meshlet.triangleCount )
    {
        gl_PrimitiveTriangleIndicesEXT[tri1_ID] = GetTri( meshlet, baseBuffer, tri1_ID );
    }
    
    uint tri2_ID = 2 * threadID + 1;
    if ( tri2_ID < meshlet.triangleCount )
    {
        gl_PrimitiveTriangleIndicesEXT[tri2_ID] = GetTri( meshlet, baseBuffer, tri2_ID );
    }
}
