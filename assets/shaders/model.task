#version 450

#extension GL_EXT_mesh_shader : require

#include "global_descriptors.glsl"
#include "c_shared/model.h"
#include "c_shared/cull_data.h"
#include "lib/intrinsics.glsl"
#include "lib/culling.glsl"
#include "c_shared/dvar_defines.h"

DEFINE_STANDARD_BUFFER_REFERENCE( 4, CullDataBuffer, PackedMeshletCullData );
DEFINE_STANDARD_BUFFER_REFERENCE( 4, MeshDataBuffer, PerObjectData );
DEFINE_STANDARD_BUFFER_REFERENCE( 4, MeshletDrawCommandBuffer, MeshletDrawCommand );

#extension GL_ARB_shader_draw_parameters : enable

layout(scalar, push_constant) uniform Registers
{
    MeshDataBuffer meshDataBuffer;
    MeshletDrawCommandBuffer indirectMeshletDrawBuffer;
};


MeshletCullData GetMeshletCullData( uint meshletID, const PerObjectData objData )
{
    uint64_t ptr = bindlessBuffers[objData.bindlessRangeStart + MESH_BUFFER_MESHLET_CULL_DATA];
    CullDataBuffer cBuffer = CullDataBuffer( ptr );
    PackedMeshletCullData packedData = cBuffer.data[meshletID];
    
    MeshletCullData cullData;
    //cullData.position.xy = unpackHalf2x16( packedData.positionXY );
    //vec2 tmp = unpackHalf2x16( packedData.positionZAndRadius );
    //cullData.position.z = tmp.x;
    //cullData.radius = tmp.y;
    //
    //vec4 tmp2 = unpackSnorm4x8( packedData.coneAxisAndCutoff );
    //cullData.coneAxis = tmp2.xyz;
    //cullData.coneCutoff = tmp2.w;
    //
    //cullData.coneApex = packedData.coneApex;

    cullData.position = packedData.position;
    cullData.radius = packedData.radius;
    cullData.coneAxis = packedData.coneAxis;
    cullData.coneCutoff = packedData.coneCutoff;
    cullData.coneApex = packedData.coneApex;
    
    return cullData;
}

bool IsMeshletVisible( const uint modelIndex, const MeshletCullData meshletCullData )
{
    if ( !IsMeshletFrustumCullingEnabled() && !IsMeshletFrustumCullingEnabled() )
        return true;

    const mat4 M         = GetModelMatrix( modelIndex );
    vec3 worldPos        = vec3( M * vec4( meshletCullData.position, 1 ) );
    vec3 componentRadius = mat3( M ) * vec3( meshletCullData.radius );
    float worldRadius    = max( componentRadius.x, max( componentRadius.y, componentRadius.z ) );

    if ( IsMeshletFrustumCullingEnabled() )
    {
        if ( !IsSphereInFrustum( worldPos, worldRadius ) )
            return false;
    }

    if ( IsMeshletBackfaceCullingEnabled() )
    {
        ////dot(center - camera_position, cone_axis) >= cone_cutoff * length(center - camera_position) + radius
        //const mat4 N       = GetNormalMatrix( modelIndex );
        //vec3 worldConeAxis = normalize( mat3( N ) * meshletCullData.coneAxis );
        //vec3 camToMeshlet  = worldPos - globals.cullingCameraPos.xyz;
        //if ( dot( camToMeshlet, worldConeAxis ) >= meshletCullData.coneCutoff * length( camToMeshlet ) + worldRadius )
        //    return false;

        vec3 worldApex = vec3( M * vec4( meshletCullData.coneApex, 1 ) );
        const mat4 N       = GetNormalMatrix( modelIndex );
        vec3 worldConeAxis = normalize( mat3( N ) * meshletCullData.coneAxis );
        if ( dot( normalize( worldApex - globals.cullingCameraPos.xyz ), worldConeAxis ) >= meshletCullData.coneCutoff )
            return false;
    }
    
    return true;
}

struct Task
{
    uint meshIndex;   
    uint baseID;
    uint8_t deltaIDs[TASK_SHADER_WORKGROUP_SIZE];
};

taskPayloadSharedEXT Task OUT;

layout(local_size_x = TASK_SHADER_WORKGROUP_SIZE) in;

void main()
{
    const uint baseMeshletID   = gl_WorkGroupID.x * TASK_SHADER_WORKGROUP_SIZE;
    const uint localMeshletID  = gl_LocalInvocationID.x;
    const uint globalMeshletID = baseMeshletID + localMeshletID;
    
    const uint numMeshlets = indirectMeshletDrawBuffer.data[gl_DrawIDARB].numMeshlets;
    const uint modelIndex  = indirectMeshletDrawBuffer.data[gl_DrawIDARB].modelIndex;
    const uint meshIndex   = indirectMeshletDrawBuffer.data[gl_DrawIDARB].meshIndex;
    
    if ( globalMeshletID >= numMeshlets )
        return;
    
    MeshletCullData cullData = GetMeshletCullData( globalMeshletID, meshDataBuffer.data[meshIndex] );
    
    bool visible = IsMeshletVisible( modelIndex, cullData );
    uint numVisibleInWave = WaveActiveCountBits( visible );
    uint localIndexInWave = WavePrefixCountBits( visible );
    if ( visible )
    {
        OUT.deltaIDs[localIndexInWave] = uint8_t(localMeshletID);
    }
    
    if ( gl_LocalInvocationID.x == 0 )
    {
        OUT.meshIndex = meshIndex;
        OUT.baseID = baseMeshletID;
    }
    EmitMeshTasksEXT( numVisibleInWave, 1, 1 );
}
