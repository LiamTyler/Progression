#version 450

#extension GL_EXT_mesh_shader : require

#include "c_shared/clay_ui.h"

layout( std430, push_constant ) uniform UIElementConstantBufferUniform
{
    layout( offset = 0 ) UIRectElementData drawData;
};

layout( local_size_x = 4, local_size_y = 1, local_size_z = 1 ) in;
layout( triangles, max_vertices = 32, max_primitives = 32 ) out;

void main()
{
    const uint threadID = gl_LocalInvocationIndex;
    SetMeshOutputsEXT( 4, 2 );
    
    vec2 pos = drawData.pos;
    pos.x += drawData.dimensions.x * float(threadID > 1);
    pos.y += drawData.dimensions.y * float(threadID == 1 || threadID == 2);
    gl_MeshVerticesEXT[threadID].gl_Position = drawData.projMatrix * vec4( 2 * pos - vec2( 1 ), 0, 1 );
    
    if ( threadID == 0 )
        gl_PrimitiveTriangleIndicesEXT[threadID] = uvec3( 0, 1, 2 );
    else if ( threadID == 1 )
        gl_PrimitiveTriangleIndicesEXT[threadID] = uvec3( 0, 2, 3 );
}   