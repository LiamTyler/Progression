cmake_minimum_required (VERSION 3.7.2)

Project(ImageCompressor)
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(helpful_functions)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_SKIP_INSTALL_ALL_DEPENDENCY true)


set(LINUX_PROGRAM   "NOT_IN_USE")
set(WINDOWS_PROGRAM "NOT_IN_USE")
set(APPLE_PROGRAM   "NOT_IN_USE")
if(UNIX AND NOT APPLE)
    set(LINUX_PROGRAM   "IN_USE")
elseif(WIN32)
    set(WINDOWS_PROGRAM "IN_USE")
elseif(APPLE)
    set(APPLE_PROGRAM   "IN_USE")
endif()
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/cmake/platform_defines.hpp.in ${CMAKE_CURRENT_SOURCE_DIR}/src/platform_defines.hpp)

set(CMAKE_CONFIGURATION_TYPES "Debug;Release")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG ${CMAKE_BINARY_DIR}/bin)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ${CMAKE_BINARY_DIR}/bin)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${CMAKE_BINARY_DIR}/lib)


find_package(OpenMP REQUIRED)
add_subdirectory(ext/compressonator/cmp_core)

file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
# Configure and build bc7enc_rdo debug and release and put the build files into the bin and lib dirs
CONFIG_TIME_COMPILE_DEBUG_AND_RELEASE(${CMAKE_SOURCE_DIR}/ext/bc7enc_rdo ${CMAKE_BINARY_DIR}/ext/bc7enc)
COPY_BUILD_FILES(${CMAKE_BINARY_DIR}/ext/bc7enc ${CMAKE_BINARY_DIR}/bin ${CMAKE_BINARY_DIR}/lib bc7enc)

# Configure and build libtiff release only (dynamic lib) and put the build files into the bin and lib dirs
CONFIG_TIME_COMPILE(${CMAKE_SOURCE_DIR}/ext/libtiff ${CMAKE_BINARY_DIR}/ext/libtiff Release)
COPY_BUILD_FILES(${CMAKE_BINARY_DIR}/ext/libtiff/libtiff/Release ${CMAKE_BINARY_DIR}/bin ${CMAKE_BINARY_DIR}/lib tiff)

set(
	SRC
    ${CMAKE_CURRENT_SOURCE_DIR}/src/assert.hpp
	${CMAKE_CURRENT_SOURCE_DIR}/src/bc_compression.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/bc_compression.hpp
	${CMAKE_CURRENT_SOURCE_DIR}/src/bc_decompression.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/bc_decompression.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/bc7_reference_tables.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/core_defines.hpp
	${CMAKE_CURRENT_SOURCE_DIR}/src/image.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/image.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/math.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/pixel_formats.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/pixel_formats.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/platform_defines.hpp
	
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/cpu_profile.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/filesystem.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/filesystem.hpp
	${CMAKE_CURRENT_SOURCE_DIR}/src/utils/float_16.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/float_16.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/logger.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/logger.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/random.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/src/utils/random.hpp

    ${CMAKE_CURRENT_SOURCE_DIR}/src/main.cpp
)

set(
	EXTERNALS
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/memory_map/MemoryMapped.h
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/memory_map/MemoryMapped.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/stb/stb_image.h
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/stb/stb_image_write.h
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/stb/stb_image_resize.h
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/tinyexr/tinyexr.h
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/tinyexr/tinyexr.cc
)

set(
	EXTERNAL_HEADER_ONLY
	${CMAKE_CURRENT_SOURCE_DIR}/ext/bc7enc_rdo/rgbcx.cpp
	${CMAKE_CURRENT_SOURCE_DIR}/ext/bc7enc_rdo/rgbcx.h
	${CMAKE_CURRENT_SOURCE_DIR}/ext/bc7enc_rdo/bc7enc.cpp
	${CMAKE_CURRENT_SOURCE_DIR}/ext/bc7enc_rdo/bc7enc.h
	${CMAKE_CURRENT_SOURCE_DIR}/ext/bc7enc_rdo/bc7e_ispc.h
	
	${CMAKE_CURRENT_SOURCE_DIR}/ext/libtiff/libtiff/tiff.h
	${CMAKE_CURRENT_SOURCE_DIR}/ext/libtiff/libtiff/tiffio.h
)
set_source_files_properties(${EXTERNAL_HEADER_ONLY} PROPERTIES HEADER_FILE_ONLY TRUE)

set(SRC_ALL ${SRC} ${EXTERNALS} ${EXTERNAL_HEADER_ONLY})
source_group(TREE ${CMAKE_SOURCE_DIR} FILES ${SRC_ALL})


add_executable(${PROJECT_NAME} ${SRC_ALL})
SET_TARGET_POSTFIX(${PROJECT_NAME})
SET_TARGET_COMPILE_OPTIONS_DEFAULT(${PROJECT_NAME})
SET_TARGET_AS_DEFAULT_VS_PROJECT(${PROJECT_NAME})

target_include_directories(${PROJECT_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/src/
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/libtiff/libtiff/
    ${CMAKE_BINARY_DIR}/ext/libtiff/libtiff/
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/compressonator
)
target_link_directories(${PROJECT_NAME} PRIVATE
	${CMAKE_BINARY_DIR}/lib/
)
target_link_libraries(${PROJECT_NAME} PRIVATE
	OpenMP::OpenMP_CXX tiff
    debug bc7enc_d optimized bc7enc
    debug CMP_Core_d optimized CMP_Core
)

